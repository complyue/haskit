import * 'net'

import * './debug'


export {
  symbol @haskItServer
  symbol @haskItPeer
}


{## The meta information for a mounted servlet #}
export data Servlet(service, entryUrl, ***extras) pass


export class HaskItServer {

  # a stack of overlayed edh modules providing web resources
  WEB_MODULES = (
    'haskit/web', # from haskit
    'dim/web', # from hasdim
    'swarm/web', # from sedh
    'net/web', # from nedh
    'web', # from edh
  )

  # WebSocket serving module
  WS_MODULE = 'haskit/web/ws'

  # extra routes by url
  URL_ROUTES = ()


  # constructor
  method __init__(
    netInterface= '127.0.0.1',
    httpPort= 3780,
    wsPort= 3790,
    numAltPorts= 9,
  ) {

    # collection of active servlets by path
    this._wsServlets = {}
    # collection of active websocket clients with their respecitive handling service module scope
    this._wsClients = {}

    this.ws = WsServer(
      that.WS_MODULE, netInterface, wsPort,
      port'max= wsPort+numAltPorts,
      init= modu => {
        modu.@haskItServer = this
      },
    )
    case this.ws.addrs() of {
      { wsAddr :> _extraWsAddrs } -> {
        console.info<| 'HaskIt WebSocket service listening: ws://'
        ++ wsAddr.host() ++ ':' ++ wsAddr.port()
      }
      error( 'HaskIt WebSocket service failed start serving.' )
    }

    this.http = HttpServer(
      that.WEB_MODULES,
      netInterface, httpPort, routes=
      # tell web browser our port of WebSocket at uri `/:`
      ( ":", '' ++ wsAddr.port(), mime= 'text/plain' )
      :> that.URL_ROUTES,
      port'max= httpPort+numAltPorts,
    )
    case this.http.addrs() of {
      { httpAddr :> _extraHttpAddrs } -> {
        console.info<| 'HaskIt http service listening: http://'
        ++ httpAddr.host() ++ ':' ++ httpAddr.port()
      }
      error( 'HaskIt http service failed start serving.' )
    }

  }


  {##
   # Enumerate all websocket clients connected to the specified path, those not hooked by a servlet
   #}
  generator wsClientsOn( path ) case this._wsClients[path] of
  # nil means no such path ever connected, we only care non-nil case
  { clntsOnPath } -> for (wsPeer, svcScope) from clntsOnPath do {
    wsPeer =: Peer()
    ;| wsPeer.eol() -> clntsOnPath[wsPeer] = nil
    effect @haskItPeer = wsPeer
    yield (wsPeer, svcScope)
  }


  {##
   # For the client facing service module to register the websocket connection it is handling

This must be called from such a service module's module procedure, thus run on its connection handling thread.
   #}
  interpreter hookWsClient( svcScope ) {
    # peer/reqPath/repParams ets. should have been implanted there
    peer = svcScope.eval(expr peer)
    reqPath = svcScope.eval(expr reqPath)

    # hskiDebug.replHere()

    # check if a servlet mounted for it
    case this._wsServlets[reqPath] of { Servlet(service) } -> {
      # hook this client to the servlet
      return service( svcScope )
    }

    # no servlet mounted for it, treat as active client session alone
    clntsOnPath = this._wsClients[reqPath] |= {}
    # cleanup on termination of the service module procedure thread
    defer clntsOnPath[peer] = nil
    # record it
    clntsOnPath[peer] = svcScope
    return false
  }


  {##

`servlet` should take the service module scope as the only arg, it'll be called on the serive thread, and can hold the client connection abitrarily long without synchronous return; once it decides to return, it should return a bool to indicate whether the client should be disconnected immediately, or continue being served.

The servlet can repeat `@netPeer.readCommand()` or `@netPeer.readSource()` before return, after it returns `false`, the client will continue being served by `readCommand()` looping within the service module scope; if it returns `true`, the client will be disconnected immediately.

   #}
  method mountServlet(
    path= '/' ++ UUID(), entryUrl= None, ***extras
  ) service => {
    case this._wsServlets[path] of { Servlet() = servlet } -> {
      console.warn<| 'Replacing servlet on path [' ++ path ++ ']: ' ++ servlet
    }
    this._wsServlets[path] = Servlet(service, entryUrl, ***extras)
    return path
  }

  method unmountServlet( path ) {
    this._wsServlets[path] = nil
  }

  generator mountedServlets() {
    for (path, servlet) from this._wsServlets do yield (path, servlet)
  }


  method join() {
    this.ws.join()
    this.http.join()
  }

  method stop() {
    this.http.stop()
    this.ws.stop()
  }

}
