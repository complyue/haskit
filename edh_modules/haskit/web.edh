
export class HaskItServer {

  forEachHaskItClient :: ( effects :: {
      # bound server instance of this method
      hskiServer :: HaskItServer
      # the peer object connected through WebSocket,
      # same as the peer object passed to the action
      hskiPeer :: Peer
      # standard data channel sink by Nedh convention
      @dataSink :: EventSink
      # also the same peer object as `hskiPeer`, by Nedh convention
      @netPeer :: Peer
    } => Peer -> Any ) -> nil
  # send the specified action to be perceived thus performed on each web
  # client's serving thread (goroutine)
  export method forEachHaskItClient( perWebClientAction ) {
    this.wcaSink <- perWebClientAction
    return nil
  }

  registerWebClient :: Peer -> nil
  # meant to be called from a web client's serving thread, to which some event
  # perceiver will be installed, the `wsPeer` passed to here should be a
  # WebSocket connection established with the root browser window of the client
  method registerWebClient( wsPeer ) {
    perceive this.wcaSink { wcAction } -> {
      wcAction( wsPeer )
    }
  }

  method __init__(
    netInterface= '127.0.0.1',
    httpPort= 3780,
    wsPort= 3790,
    numAltPorts= 9,
  ) {

    # the sink for web client actions to be performed on demand
    this.wcaSink = sink

    # locally import Nedh stuff, not needed elsewhere
    import * 'net'

    this.ws = WsServer(
      'haskit/web/ws', netInterface, wsPort,
      port'max= wsPort+numAltPorts,
      init= modu => {
        modu.hskiServer = this
      },
    )
    case this.ws.addrs() of {
      { wsAddr :> _extraWsAddrs } -> {
        console.info<| 'HaskIt WebSocket service listening: ws://'
        ++ wsAddr.host() ++ ':' ++ wsAddr.port()
      }
      error( 'HaskIt WebSocket service failed start serving.' )
    }

    this.http = HttpServer(
      ( # a stack of overlayed edh modules providing web resources
        'haskit/web', # from haskit
        'dim/web', # from hasdim
        'swarm/web', # from sedh
        'net/web', # from nedh
        'web', # from edh
      ),
      netInterface, httpPort, routes= (
        # tell web browser our port of WebSocket at uri `/:`
        ( ":", '' ++ wsAddr.port(), mime= 'text/plain' ),
      ), port'max= httpPort+numAltPorts,
    )
    case this.http.addrs() of {
      { httpAddr :> _extraHttpAddrs } -> {
        console.info<| 'HaskIt http service listening: http://'
        ++ httpAddr.host() ++ ':' ++ httpAddr.port()
      }
      error( 'HaskIt http service failed start serving.' )
    }

  }

  method join() {
    this.ws.join()
    this.http.join()
  }

  method stop() {
    this.http.stop()
    this.ws.stop()
  }

}
